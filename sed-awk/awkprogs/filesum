ls -l $* | awk ' # 通过(管道)把ls输出导给awk
# filesum: list files and total size in bytes
# input: long listing produced by "ls -l"
# (==) (&&) (~)

#1 output column headers
BEGIN {
    printf("%-15s\t%10s\n","FILE","BYTES" )
}


#2 test for 8 fields; files begin with "-"
NF == 8 && /^-/ {
    sum += $5       # accumulate size of file
    ++filenum       # count number of files
    printf("%-15s\t%10d\n", $8, $5)       # print filename and size
}

#3 test for 8 fields; directory begins with "d"
NF == 8 && /^d/ {
    print "<dir>", "\t", $8  #print <dir> and name
}

#4 test for ls -lR line ./dir:
$1 ~ /^\..*:$/ {
    print "\t" $0 # print that line preceded by tab
}


#5 once all is done,
END {
    #print total file size and number of files
    printf("Total: %d bytes  (%d files)\n", sum, filenum)
}' - # "-" 可要可不要!
